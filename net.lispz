### Load packages from CDN and other web sources - listing them when possible ###
(using "dom"
  (var script (lambda [uri next-step=>]
    (var el (dom.element "script" {type: "text/javascript"}))
    (dom.append! "head" el)
    (el.addEventListener "load"  (lambda [evt] (delay 20 (next-step=> evt))))
    (el.addEventListener "error" (lambda [evt] (throw evt)))
    (set! el.src uri)
  ))

  (var css (lambda [uri]
    (var el (dom.element "link" {type: "text/css" rel: "stylesheet" href: uri}))
    (dom.append! "head" el)
  ))

  (var http-get (lambda [uri next-step=>]
    (lispz.http_request uri "GET" next-step=>)
  ))

  (var json-request (lambda [uri next-step=>]
    (http-get uri (lambda [response] (next-step=> (JSON.parse response.text))))
  ))

  (var cdnjs (lambda [query pkg next-step=>]
    (json-request (+ "http://api.cdnjs.com/libraries?" query pkg) (lambda [json]
      (var filtered (json.results.filter (lambda [it] (return (=== it.name pkg)))))
      (next-step=> (get filtered 0))
    ))
  ))

  ###
  # returns an array of filtered full paths to files to upload
  ###
  (var cdnjs-assets (lambda [pkg next-step=>]
    (cdnjs "fields=assets&search=" pkg (lambda [item]
      (item.assets.some (lambda [it]
        (return? (contains it.version "alpha") false)
        (set! it.base (+ "https://cdnjs.cloudflare.com/ajax/libs/" pkg "/" it.version "/"))
        (next-step=> it)
	(return true)
      ))
    ))
  ))

  (var cdnjs-load-some (lambda [asset inclusion-test=> next-step=>]
      (sequential asset.files (lambda [file next-file=>]
        (return? (or (get asset.loaded file) (not (inclusion-test=> file))) (next-file=>))
	(set! (get asset.loaded file) true)
        (var ext (last (file.split ".")))
        (var filepath (+ asset.base file))
        (lispz.manifest.push filepath)
        (cond
          (is ext "js")   (script filepath next-file=>)
          (is ext "css")  (do (css filepath) (next-file=>))
	  (else)          (next-file=>)
        )
      ) next-step=>)
  ))

  (var inclusion (lambda [match] (return (lambda [file] (return (match.test file))))))
  (var exclusion (lambda [match] (return (lambda [file] (return (not (match.test file)))))))
  
  (var cdn-cache {})

  ## matches is an array. if global flag is set it is inclusive, else exclusive
  (var cdnjs-load (lambda [pkg matches next-step=>]
    (return? (get cdn-cache pkg) (next-step=>))
    (set! (get cdn-cache pkg) true)
    (cdnjs-assets pkg (lambda [asset]
      (set! asset.loaded {})
      (sequential matches (lambda [match next-match=>]
        ## RE with global flat is positive test, without is negative (exclude) 
        (cond match.global (cdnjs-load-some asset (inclusion match) next-match=>)
	      (else)       (cdnjs-load-some asset (exclusion match) next-match=>))
      ) next-step=>)
    ))
  ))

  (export {script css http-get json-request cdnjs-load})
)
