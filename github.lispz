### https://developer.github.com/v3/ ###
### spec: Support
  (using [github]
    (ref delete-file> (lambda [path]
      (ref repo (github.repo> "paulmarrington/lispz"))
      (after (github.contents> repo "master" "README.md")
        (github.delete> repo "master" @.sha "deleted")
      )
    ))
    (ref get-sha> (lambda [path]
      (ref repo (github.repo> "paulmarrington/lispz"))
      (after (github.contents> repo "master" path) @.sha)
    ))
    (ref create-file> (lambda [path]
      (ref repo (github.repo> "paulmarrington/lispz"))
      (after (github.create> repo "master" path "Hi" "created") @ ))
    ))
  )
###
(using [net dict dom]

  ### spec: Retrieve repository details
    (it "will include the full name of the repo" (lambda [done]
      (using [github]
        (after (github.repo> "paulmarrington/lispz")
        	((expect @.full_name).toEqual "paulmarrington/lispz")
          (done)
        )
      )
    ))
  ###
  ### ref: (github.repo> project) ## log in form displayed if necessary
    e.g. (after (github.repo> "paulmarrington/lispz") ...)
    https://developer.github.com/v3/repos/contents/#get-contents
  ###
  (ref repo> (lambda [name] (get> (+ "repos/" name) {})))

  ### spec: List repositories for a user
    (it "will include the full name of the repo" (lambda [done]
      (using [github]
        (after (github.repos> "paulmarrington")
        	((expect @.length).toBeGreaterThan 7)
          (done)
        )
      )
    ))
  ###
  ### ref: (github.repos> organisation)
    e.g. (after (github.repos> "paulmarrington") ...)
    https://developer.github.com/v3/repos/#list-user-repositories
  ###
  (ref repos> (lambda [owner]
    (get> (cond owner (+ "users/" owner "/repos") (else) "user/repos") {
      affiliation: "owner,collaborator" sort: "full-name"
    })
  ))

  ### spec: Read contents given a branch and path
    (it "can be read as base64" (lambda [done]
      (using [github]
        (ref (github.repo> "paulmarrington/lispz"))
        (after (github.contents> repo "master" "README.md")
        	((expect @.encoding).toEqual "base64")
          (done)
        )
      )
    ))
  ###
  ### ref: (github.contents> <repo> branch path) ## info on dir or file
    e.g. (after (github.contents> repo "master" "/lib" }))
    https://developer.github.com/v3/repos/contents/#get-contents
  ###
  (ref contents> (lambda [repo branch path]
    (when [repo]
      (get> (+ "repos/" repo.full_name "/contents/" path) { ref: branch })
    )
  ))

  ### spec: Read contents given SHA
    (it "can be read as base64" (lambda [done]
      (using [github]
        (ref repo (github.repo> "paulmarrington/lispz"))
        (after (github.blob> repo "master" "README.md")
        	((expect @.encoding).toEqual "base64")
          (done)
        )
      )
    ))
  ###
  ### ref: (github.blob> <repo> sha)
    e.g. (after (github.blob> repo tree[path].sha) @.contents)
    https://developer.github.com/v3/git/blobs/#get-a-blob
  ###
  (ref blob> (lambda [repo sha]
    (when [repo] (get> (+ "repos/" repo.full_name "/git/blobs/" sha)))
  ))

  ### spec: Download a file from the repository
    (it "can be read in text form" (lambda [done]
      (using [github]
        (ref repo (github.repo> "paulmarrington/lispz"))
        (after (github.read> repo "master" "README.md")
        	((expect @).toEqual "base64")
          (done)
        )
      )
    ))
  ###
  ### ref: (github.read> <repo> branch path)
    e.g. (after (github.read> repo "master" "README.md") ...)
  ###
  (ref read> (lambda [repo branch path] (when [repo]
    (ref base "https://raw.githubusercontent.com")
    (ref url (+ base "/" repo.full_name "/" branch "/" path))
    (net.http-get url)
  )))

  ### spec: Create a new entry
    (it "can create a new entry" (lambda [done]
      (using [github]
        (ref repo (github.repo> "paulmarrington/lispz"))
        ((after (github.create> repo "master" "test/tmp.txt" "Hi" "msg")
        	((expect @.message).toEqual "msg")
          (done)
        ).catch (=> (fail "Can't create -" repo) (done)))
      )
    ))
  ###
  ### ref: (github.create> <repo> branch path contents commit-message)
    (after (github.create> repo "master" "test/tmp.txt" "Hi" "msg") ...)
    https://developer.github.com/v3/repos/contents/#create-a-file
  ###
  (ref create> (lambda [repo branch path contents message] (when [repo]
    (ref url (+ "repos/" repo.full_name "/contents/" path))
    (ref options { branch message content: (net.base64 contents) })
    (request> "PUT" url options)
  )))

  ### spec: Update an entry
    (it "can update an entry" (lambda [done]
      (using [github]
        (ref create (create-file> "test/tmp.txt" "created"))
        (ref sha (get-sha> "test/tmp.txt"))
        (ref repo (github.repo> "paulmarrington/lispz")
        ((when [create sha repo]
          (after (github.update> repo "master" "test/tmp.txt" sha "2nd" "changed")
            ((expect @.message).toEqual "changed")
          )
        ).catch (=> (fail "Can't update -" @) (done)))
      )
    ))
  ###
  ### ref: (github.update> <repo> branch path sha contents commit-message)
    (after (github.update> repo "master" "test/tmp.txt" sha "Hi" "msg") ...)
    https://developer.github.com/v3/repos/contents/#update-a-file
  ###
  (ref update> (lambda [repo branch path sha contents message] (when [repo]
    (ref url (+ "repos/" repo.full_name "/contents/" path))
    (ref options { branch message sha content: (net.base64 contents) })
    (request> "PUT" url options)
  )))

  ### spec: Delete an entry
    (it "can delete an entry" (lambda [done]
      (using [github]
        (ref create (create-file> "test/tmp.txt" "created"))
        (ref sha (get-sha> "test/tmp.txt"))
        (ref repo (github.repo> "paulmarrington/lispz")
        ((when [create sha repo]
          (after (github.delete> repo "master" "test/tmp.txt" sha "no more")
            ((expect @.commit.message).toEqual "no more")
          )
        ).catch (=> (fail "Can't update -" @) (done)))
      )
    ))
  ###
  ### ref: (github.delete> <repo> branch path sha commit-message)
    (after (github.delete> repo "master" "test/tmp.txt" sha "msg") ...)
    https://developer.github.com/v3/repos/contents/#delete-a-file
  ###
  (ref delete> (lambda [repo branch path sha message] (when [repo]
    (ref url (+ "repos/" repofull_name "/contents/" path))
    (ref options { branch message sha })
    (request> "DELETE" url options)
  )))

  ### spec: List contents of a repository
    (it "can retrieve a tree object" (lambda [done]
      (using [github]
        (ref repo (github.repo> "paulmarrington/lispz")
        (after (github.tree> repo "master")
        	((expect @.tree.length).toBeGreaterThan 100)
          (done)
        ))
      )
    ))
  ###
  ### ref: (github.tree> <repo> branch) ## recursive info tree
    e.g. (after (github.tree> repo "master"))
    https://developer.github.com/v3/git/trees/#get-a-tree-recursively
  ###
  (ref tree> (lambda [repo branch] (when [repo]
    (after (branch> repo branch)
    (get>
      (+ "repos/" repo.full_name "/git/trees/" @.commit.sha)
      { recursive: 1 }
    ))
  )))

  ### spec: Retrieve branch
    (it "can retrieve branch details" (lambda [done]
      (using [github]
        (ref repo (github.repo> "paulmarrington/lispz")
        (after (github.branch> repo "master")
        	((expect @.name).toequal "master")
          (done)
        ))
      )
    ))
  ###
  ### ref: (github.branch> <repo> [branch]) ## branch defaults to master
    e.g. (after (github.branch> repo) ...)
    https://developer.github.com/v3/repos/#get-branch
  ###
  (ref branch> (lambda [repo branch] (when [repo]
    (get> (+ "repos/" repo.full_name "/branches/" (or branch "master")))
  )))

  ### ref: (github.branches> repo) ###
  (ref branches> (lambda [repo] (when [repo]
    (get> (+ "repos/" repo.full_name "/branches"))
  )))

  ### ref: (github.session>)
    Start a GitHub session. Shows modal dialog if user-name and password
    are not in browser storage.
  ###
  (ref session> (=>
    (ref user (or
      (sessionStorage.getItem "github-user")
      (localStorage.getItem "github-user")
    ))
    (ref auth-header (or
      (sessionStorage.getItem "ghcontext")
      (localStorage.getItem "ghcontext")
    ))
    (cond
      auth-header (promised { auth-header user })
      (else)      (ask-user-to-login>)
    )
  ))

  (ref ask-user-to-login> (promise (using [bootstrap]
    (ref modal (bootstrap.modal> "github/github-login" {}))
    (when [modal]
      (cond (is modal.login.value "true") (do
        (ref joined (+ modal.username.value ":" modal.password.value))
        (ref auth-header (+ "Basic " (btoa joined)))
        (save-user modal.username.value auth-header modal.remember.checked)
        (resolve-promise { auth-header user: modal.username.value })
      ) (else)
        (reject-promise { error: true reason: "User abort" })
      )
    )
  )))
  (ref save-user (lambda [user-name auth remember]
    (cond (is remember) (do
      (localStorage.setItem "ghcontext" auth)
      (localStorage.setItem "github-user" user-name)
    ) (else) (do ## user wants amnesia
      (localStorage.removeItem "ghcontext")
      (localStorage.removeItem "github-user")
      (sessionStorage.setItem "ghcontext" auth)
      (sessionStorage.setItem "github-user" user-name)
    ))
  ))

  (ref request> (lambda [type url options]
    (ref send-request (lambda [session]
      (ref headers {
        Accept:           "application/vnd.github.v3+json"
        Authorization:    session.auth-header
        'Content-Type':   "application/json;charset=UTF-8"
      })
      (net.http-request> type
        (+ "https://api.github.com/" url) headers
        (JSON.stringify options)
      )
    ))

    (after session> (after (send-request @) (JSON.parse @)))
  ))

  (ref get> (lambda [url options]
    (request> "GET" (+ url (net.dict-to-query options)) {})
  ))

  ### ref: (github.cdn-uri project hash filepath)
    e.g. (ref uri (github.cdn-uri "atogov/RAM" "develop" "tests/pasckages.json"))
  ###
  (ref cdn-uri (lambda [project hash filepath]
    (return (+ "https://cdn.rawgit.com/" project "/" hash "/" filepath))
  ))

  ### ref: (ref fs (once github.fs repo branch)) ## entries> read> write>
    e.g. (after fs (after (@.read> "README.md") ...))
  ###
  (ref fs (lambda [name branch]
    (ref repo  (once (repo> name)))
    (ref list> (once (after (tree> (repo) fs.branch) @.tree)))
    (ref entries> (once (after (fs.list>) (dict.from-list @ "path"))))
    (ref read> (lambda [path] (github.read> (repo) fs.branch path)))
    (ref write> (lambda [path contents message]
      (after entries>
        (cond (ref meta (get @ path))
          (github.update> (repo) fs.branch path meta.sha contents message)
        (else)
          (github.create> (repo) fs.branch path contents message)
        )
      )
    ))
    (ref fs {
      name branch: (or branch "master")
      list> entries> read> write>
    })
  ))

  (ref github {
    repo> repos> cdn-uri contents> tree> branch> branches> blob> read>
    create> update> delete> fs
  })
  (export github)
)
