### spec: Packages >> External >> CDNJS
  Lispz stands on the shoulders of giants. Creating a system using Lispz will require
  access to other JavaScript and CSS packages from the Internet. The lispz packaging
  system will create, update and combine external code and save it locally. One of the
  major sources for code is [cdnjs.com](https://cdnjs.com/). It includes an API so
  that the most recent non-alpha version can be chose on create or refresh.
  
  This code uses a site specific driver supplied to a bithub builder to retrieve and
  process files. Modules that rely on an external package provide a build method. The
  resulting file can incorporate files from different repositories with commands to
  explicitly exclude or include files with regular expressions. The resulting file
  is saved to /ext in the current project.
  
      (var build (lambda [target-repo]
        (return (cdnjs.build target-repo "jquery" [[
          {repo: "jquery" files: [[{exclude: '/\.map$|\.min.js$/'}]]}
        ]]))
      ))
###
(using [net github]
  (var cdnjs-actors {
    list-all: (promise [repo path]
      (var base "http://api.cdnjs.com/libraries?fields=assets&search=")
      (when (net.json-request (+ base repo.name)) [json]
        ## select the correct repo for the name
        (var filtered (json.results.filter (lambda [it]
          (return (=== it.name repo.name))
        )))
        ((get filtered 0).assets.some (lambda [it]
          (return-if (contains it.version "alpha") false)
          (set! repo.base (+
            "https://cdnjs.cloudflare.com/ajax/libs/"
            repo.name "/" it.version "/"
          ))
          (resolve-promise it.files)
          (return true) ## found the one we want
        ))
      )
    )
    read: (promise [repo path]
      (var uri (+ repo.base path))
      (when (net.http-get uri) [response] (resolve-promise response))
    )
    repo: (lambda [target-repo name] (return {name}))
  })

  (export {
    build: (github.builder.bind null cdnjs-actors)
  })
)
